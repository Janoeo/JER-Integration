buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'maven-publish'

def majorVersion = 3
def minorVersion = 3
def buildVersion = 2
def isExperimental = false
version = "${majorVersion}.${minorVersion}.${buildVersion}"
if (isExperimental) {
    version += '-experimental'
}

group = 'fr.alasdiablo.jerintegration'
archivesBaseName = 'JER-Integration'

def manifestAttributes = [
        "Specification-Title"     : "JER-Integration",
        "Specification-Vendor"    : "AlasDiablo",
        "Specification-Version"   : "1",
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : project.jar.archiveVersion,
        "Implementation-Vendor"   : "AlasDiablo",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
]

def outputFolder = file(rootProject.getRootDir().getPath() + '/output')

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

def deobfSourceClassifier

minecraft {
    mappings channel: 'parchment', version: '2022.11.06-1.18.2'
    deobfSourceClassifier = getMappings().get()
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'jerintegration'
            mods {
                jerintegration {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'jerintegration'
            mods {
                jerintegration {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'jerintegration'
            mods {
                jerintegration {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'jerintegration', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                jerintegration {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url = 'https://www.cursemaven.com/' }
    maven { url 'https://maven.tterrag.com/' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.1.60'

    implementation fileTree(dir:'lib', include:'*.jar')

    implementation fg.deobf('curse.maven:jei-238222:4434393')                       // JEI (10.2.1.1004)
    implementation fg.deobf('curse.maven:just-enough-resources-jer-240630:3831559') // JER (0.14.1.171)

    implementation fg.deobf('curse.maven:immersiveengineering-231951:4412849')      // Immersive Engineering (8.4.0-161)

    implementation fg.deobf('curse.maven:create-328085:4550986')                    // Create (v0.5.1b)
    compileOnly fg.deobf('com.tterrag.registrate:Registrate:MC1.18.2-1.1.3')        // Create source dependencies

    implementation fg.deobf('curse.maven:mekanism-268560:3875976')                  // Mekanism (10.2.5.465)

    implementation fg.deobf('curse.maven:extreme-reactors-250277:4526683')          // Extreme Reactors (2.0.63)
    implementation fg.deobf('curse.maven:zerocore-247921:4526166')                  // ZeroCore 2 (2.1.34)

    implementation fg.deobf("curse.maven:randomite-classic-375134:3680209")         // Randomite Classic (v1.1.2)

    implementation fg.deobf('curse.maven:mantle-74924:4509007')                     // Mantle (1.9.45)
    implementation fg.deobf('curse.maven:tinkers-construct-74072:4509008')          // Tinkers' Construct (3.6.4.113)

    implementation fg.deobf("curse.maven:mining-master-534956:4041440")             // Mining Master (4.0.0)

    implementation fg.deobf("curse.maven:applied-energistics-2-223794:4548560")     // Applied Energistics 2 (11.7.4)

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest.attributes(manifestAttributes)
    destinationDirectory.set(file(rootProject.getRootDir().getPath() + '/output'))
}

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    def archiveName = 'sources-' + deobfSourceClassifier
    archiveClassifier.set(archiveName)
    destinationDirectory.set(outputFolder)
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    def archiveName = 'deobf-' + deobfSourceClassifier
    archiveClassifier.set(archiveName)
    manifest.attributes(manifestAttributes)
    destinationDirectory.set(outputFolder)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

artifacts {
    archives jar
    archives sourcesJar
    archives deobfJar
}